cmake_minimum_required(VERSION 3.19)

set(rocsift_known_comps static shared)
set(rocsift_comp_static NO)
set(rocsift_comp_shared NO)
foreach (rocsift_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (rocsift_comp IN_LIST rocsift_known_comps)
        set(rocsift_comp_${rocsift_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "rocsift does not recognize component `${rocsift_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (rocsift_comp_static AND rocsift_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "rocsift `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(rocsift_static_targets "${CMAKE_CURRENT_LIST_DIR}/rocsift-static-targets.cmake")
set(rocsift_shared_targets "${CMAKE_CURRENT_LIST_DIR}/rocsift-shared-targets.cmake")

macro(rocsift_load_targets type)
    if (NOT EXISTS "${rocsift_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "rocsift `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${rocsift_${type}_targets}")
endmacro()

if (rocsift_comp_static)
    rocsift_load_targets(static)
elseif (rocsift_comp_shared)
    rocsift_load_targets(shared)
elseif (DEFINED ROCSIFT_SHARED_LIBS AND ROCSIFT_SHARED_LIBS)
    rocsift_load_targets(shared)
elseif (DEFINED ROCSIFT_SHARED_LIBS AND NOT ROCSIFT_SHARED_LIBS)
    rocsift_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${rocsift_shared_targets}")
        rocsift_load_targets(shared)
    else ()
        rocsift_load_targets(static)
    endif ()
else ()
    if (EXISTS "${rocsift_static_targets}")
        rocsift_load_targets(static)
    else ()
        rocsift_load_targets(shared)
    endif ()
endif ()
